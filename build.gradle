plugins {
    id 'java'
    id 'maven-publish'
    id 'de.gliderpilot.semantic-release' version '1.4.2'
    id 'com.github.blueboxware.tocme' version '1.3'
}

/*
 * This file isn't uploaded to GitHub.
 * At the moment, it just contains the GitHub token for the semantic release plugin,
 * so you should come along very well without it. :)
 */
if (file('secrets.gradle').exists()) {
    apply from: 'secrets.gradle'
}
else {
    //Possibly we're using GitHub Actions, where the token is set automatically:
    ext.ghToken =  "$System.env.GITHUB_TOKEN"
    ext.ghActor =  "$System.env.GITHUB_ACTOR"
}


group='com.github.ahuemmer'

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url = 'https://jitpack.io'
    }
    maven {
        url = 'https://repo.maven.apache.org/maven2'
    }
    maven {
        url = 'https://maven.pkg.github.com/ahuemmer/storesthal'
        credentials {
            username = project.ghActor
            password = project.ghToken
        }
    }
}

ext {
    springVersion = '5.2.8.RELEASE'
    junitVersion = '5.6.2'
    wiremockVersion = '2.27.1'
    wiremockExtensionVersion = '0.4.0'
    springCorePluginVersion = '2.0.0.RELEASE'
    jacksonVersion = '2.11.1'
    commonsLang3Version = '3.11'
    commonsTextVersion = '1.9'
    openTestVersion = '1.2.0'
    springHateoasVersion = '1.1.1.RELEASE'
    log4jSlf4jImplVersion = '2.14.1'
}

dependencies {
    implementation group: 'org.springframework', name: 'spring-context', version: "${springVersion}"
    implementation group: 'org.springframework.hateoas', name: 'spring-hateoas', version: "${springHateoasVersion}"
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: "${jacksonVersion}"
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: "${jacksonVersion}"
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: "${jacksonVersion}"
    implementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: "${jacksonVersion}"
    implementation group: 'org.apache.commons', name:'commons-lang3', version: "${commonsLang3Version}"
    implementation group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: "${log4jSlf4jImplVersion}"

    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: "${junitVersion}"
    testImplementation group: 'com.github.tomakehurst', name: 'wiremock', version: "${wiremockVersion}"
    testImplementation group: 'com.github.JensPiegsa', name: 'wiremock-extension', version: "${wiremockExtensionVersion}"
    testImplementation group: 'org.apache.commons', name: 'commons-text', version: "${commonsTextVersion}"

    testRuntimeOnly group: 'org.opentest4j', name:'opentest4j', version: "${openTestVersion}"
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: "${junitVersion}"
}


test {
    useJUnitPlatform()
    testLogging.showStandardStreams = true
}

javadoc {
    options.encoding = 'UTF-8'
    options.charSet = 'UTF-8'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

def pomConfig = {
    scm {
        url 'https://github.com/ahuemmer/storesthal'
    }
    distributionManagement {
        repository {
            id 'github'
            name 'Storesthal Apache Maven Packages'
            url 'https://maven.pkg.github.com/ahuemmer/storesthal'
        }
    }
    servers {
        server {
            id 'github'
            username project.ghActor
            password project.ghToken
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from(components.java)
            artifact sourcesJar
            artifact javadocJar

            pom.withXml {
                asNode().children().last() + pomConfig
            }
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/ahuemmer/storesthal"
            credentials {
                username = project.ghActor
                password = project.ghToken
            }
        }
    }
}

semanticRelease {
    repo {
        ghToken = project.ghToken
        releaseAsset jar, label: 'JAR file', contentType: 'application/zip'
        releaseAsset sourcesJar, label: 'JAR file including sources', contentType: 'application/zip'
        releaseAsset javadocJar, label: 'JAR file including javadoc', contentType: 'application/zip'
    }
}

tocme {
    doc(file('README.md'))
}

tasks.release.dependsOn checkTocs